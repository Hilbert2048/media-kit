import java.io.File
import java.nio.file.Files
import java.security.MessageDigest

group 'com.alexmercerind.media_kit_libs_android_video'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.alexmercerind.media_kit_libs_android_video'
    }

    compileSdkVersion 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
    }

    dependencies {
        implementation fileTree(dir: "$buildDir/output", include: "*.jar")
    }
}

task downloadDependencies(type: Exec) {   
        // Delete previously created /output subdirectory.

        def outputDir = file("$buildDir/output")
        if (outputDir.exists()) {
            outputDir.deleteDir()
        }
        outputDir.mkdirs()

        // CHANGE THIS TAG to match your GitHub Release
        // The artifacts will be downloaded into a directory specific to this tag.
        def tag = "v0.5.0-preload" 
        def baseUrl = "https://github.com/Hilbert2048/libmpv-android-video-build/releases/download/${tag}"
        // Download 'encoders-gpl' jars which include FFmpeg CLI features and GPL codecs.
        def filesToDownload = [
            ["url": "${baseUrl}/default-arm64-v8a.jar", "sha256": "b8a2a8de3af6988e8920982d9ae9854611c38c52b6f3a32ae06fa2e36c6f498f", "destination": file("$buildDir/${tag}/default-arm64-v8a.jar")],
            ["url": "${baseUrl}/default-armeabi-v7a.jar", "sha256": "fe84f6434e4c2d7feadd605dafca4faca3822403f6f2f872fd41070652579c55", "destination": file("$buildDir/${tag}/default-armeabi-v7a.jar")],
            ["url": "${baseUrl}/default-x86.jar", "sha256": "a33bdd42aa00095bd8fe2923aae99e0bdad0bd9fea9457d829fbbcf433339aa2", "destination": file("$buildDir/${tag}/default-x86.jar")],
            ["url": "${baseUrl}/default-x86_64.jar", "sha256": "ee0050a07811c5e4803b103b35937f1b002a53cd5d655b33004c871051f2b941", "destination": file("$buildDir/${tag}/default-x86_64.jar")]
        ]
        filesToDownload.each { fileInfo ->
            def destFile = fileInfo.destination
            if (destFile.exists()) {
                def calculatedHash = MessageDigest.getInstance("SHA-256").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()

                if (calculatedHash != fileInfo.sha256) {
                    destFile.delete()
                    println "Checksum mismatch. File deleted: ${destFile}"
                }
            }

            if (!destFile.exists()) {
                destFile.parentFile.mkdirs()
                println "Downloading file from: ${fileInfo.url}"
                destFile.withOutputStream { os ->
                    os << new URL(fileInfo.url).openStream()
                }

                def calculatedHash = MessageDigest.getInstance("SHA-256").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()
                if (calculatedHash != fileInfo.sha256) {
                    throw new GradleException("SHA-256 verification failed for ${destFile}")
                }
            }

            copy {
                from destFile
                into outputDir
            }
        }
}

assemble.dependsOn(downloadDependencies)