import java.io.File
import java.nio.file.Files
import java.security.MessageDigest

group 'com.alexmercerind.media_kit_libs_android_video'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.alexmercerind.media_kit_libs_android_video'
    }

    compileSdkVersion 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
    }

    dependencies {
        implementation fileTree(dir: "$buildDir/output", include: "*.jar")
    }
}

task downloadDependencies(type: Exec) {   
        // Delete previously created /output subdirectory.

        def outputDir = file("$buildDir/output")
        if (outputDir.exists()) {
            outputDir.deleteDir()
        }
        outputDir.mkdirs()

        def tag = "v0.5.3"
        def baseUrl = "https://github.com/Hilbert2048/libmpv-android-video-build/releases/download/${tag}"

        def filesToDownload = [
            ["url": "${baseUrl}/default-arm64-v8a.jar", "sha256": "aacfddb9067423afa0bc9b2718b08c148cb07965dbafcbddbe5604579bd3b3ea", "destination": file("$buildDir/${tag}/default-arm64-v8a.jar")],
            ["url": "${baseUrl}/default-armeabi-v7a.jar", "sha256": "f090a551e21a231adb5027b4ef74ba7ca4335b8193052e6e1742db8e60e9a083", "destination": file("$buildDir/${tag}/default-armeabi-v7a.jar")],
            ["url": "${baseUrl}/default-x86.jar", "sha256": "9bd4fb2a13318329601b9f766461adbf6038b6248eac50f818cec6cb9b8fe52e", "destination": file("$buildDir/${tag}/default-x86.jar")],
            ["url": "${baseUrl}/default-x86_64.jar", "sha256": "aa03bea8ce05cfb0d9cfb5e18ace0b311ffc50daaf9103fafedd2b9484bb208d", "destination": file("$buildDir/${tag}/default-x86_64.jar")]
        ]
        filesToDownload.each { fileInfo ->
            def destFile = fileInfo.destination
            if (destFile.exists()) {
                def calculatedHash = MessageDigest.getInstance("SHA-256").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()

                if (calculatedHash != fileInfo.sha256) {
                    destFile.delete()
                    println "Checksum mismatch. File deleted: ${destFile}"
                }
            }

            if (!destFile.exists()) {
                destFile.parentFile.mkdirs()
                println "Downloading file from: ${fileInfo.url}"
                destFile.withOutputStream { os ->
                    os << new URL(fileInfo.url).openStream()
                }

                def calculatedHash = MessageDigest.getInstance("SHA-256").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()
                if (calculatedHash != fileInfo.sha256) {
                    throw new GradleException("SHA-256 verification failed for ${destFile}")
                }
            }

            copy {
                from destFile
                into outputDir
            }
        }
}

assemble.dependsOn(downloadDependencies)