import java.io.File
import java.nio.file.Files
import java.security.MessageDigest

group 'com.alexmercerind.media_kit_libs_android_video'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.alexmercerind.media_kit_libs_android_video'
    }

    compileSdkVersion 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
    }

    dependencies {
        implementation fileTree(dir: "$buildDir/output", include: "*.jar")
    }
}

task downloadDependencies(type: Exec) {   
        // Delete previously created /output subdirectory.

        def outputDir = file("$buildDir/output")
        if (outputDir.exists()) {
            outputDir.deleteDir()
        }
        outputDir.mkdirs()

        // CHANGE THIS TAG to match your GitHub Release
        // The artifacts will be downloaded into a directory specific to this tag.
        def tag = "vnext" 
        def baseUrl = "https://github.com/Hilbert2048/libmpv-android-video-build/releases/download/${tag}"

        // Download 'encoders-gpl' jars which include FFmpeg CLI features and GPL codecs.
        def filesToDownload = [
            ["url": "${baseUrl}/encoders-gpl-arm64-v8a.jar", "sha256": "28353d82f95eeadd592bc99ba329b8fab6c7b0faba2e95f04c8f358525b05d6d", "destination": file("$buildDir/${tag}/encoders-gpl-arm64-v8a.jar")],
            ["url": "${baseUrl}/encoders-gpl-armeabi-v7a.jar", "sha256": "38f518e25497db3014c0987c7f13128bd46203cfeb65266b24b8bff58cf0d247", "destination": file("$buildDir/${tag}/encoders-gpl-armeabi-v7a.jar")],
            ["url": "${baseUrl}/encoders-gpl-x86_64.jar", "sha256": "7e6e67f189888ac705916731103f2f72f754fbbb8d5c5870527fc26d38a1a71f", "destination": file("$buildDir/${tag}/encoders-gpl-x86_64.jar")],
            ["url": "${baseUrl}/encoders-gpl-x86.jar", "sha256": "c67eeda35905ba38194dabb9151d06fdf107c6ff2420ed065d1b2b5126632cbc", "destination": file("$buildDir/${tag}/encoders-gpl-x86.jar")]
        ]

        filesToDownload.each { fileInfo ->
            def destFile = fileInfo.destination
            if (destFile.exists()) {
                def calculatedHash = MessageDigest.getInstance("SHA-256").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()

                if (calculatedHash != fileInfo.sha256) {
                    destFile.delete()
                    println "Checksum mismatch. File deleted: ${destFile}"
                }
            }

            if (!destFile.exists()) {
                destFile.parentFile.mkdirs()
                println "Downloading file from: ${fileInfo.url}"
                destFile.withOutputStream { os ->
                    os << new URL(fileInfo.url).openStream()
                }

                def calculatedHash = MessageDigest.getInstance("SHA-256").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()
                if (calculatedHash != fileInfo.sha256) {
                    throw new GradleException("SHA-256 verification failed for ${destFile}")
                }
            }

            copy {
                from destFile
                into outputDir
            }
        }
}

assemble.dependsOn(downloadDependencies)